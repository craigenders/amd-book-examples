! EXAMPLE 3.10 - FIML Regression w Auxiliary Variables (Extra DV)

DATA:
file = drugtrial.dat;
VARIABLE:
names = id male drug severe0 severe1 severe3 severe6 dropgrp 
     edrop ldrop dropout sdrop3 sdrop6;
usevariables = male drug severe0 severe1 severe3 severe6;
missing = all(999);
DEFINE:
center severe0 male (grandmean);
ANALYSIS:
estimator = mlr;
MODEL:
! factored (sequential) specification for predictors;
severe0 on drug male;
! label slope coefficients in focal model;
severe6 on drug (b1)
	severe0 (b2)
 	male (b3);
! auxiliary variables as extra dependent variables with correlated errors;
severe1 on severe0 drug male;
severe3 on severe0 drug male;
severe1 severe3 severe6 with severe1 severe3 severe6;
MODEL TEST:
! wald test that all slopes equal 0;
b1 = 0;
b2 = 0;
b3 = 0;
OUTPUT:
patterns sampstat stdyx cinterval;