! EXAMPLE 9.8 - FIML Regression w MNAR Pattern Mixture Model (Diffuse)

DATA:
file = drugtrial.dat;
VARIABLE:
names = id male drug severe0 severe1 severe3 severe6 dropgrp 
     edrop ldrop dropout sdrop3 sdrop6;
usevariables = male drug severe0 severe1 severe3 severe6 dropout drugbydrop;
categorical = dropout;
missing = all(999);
DEFINE:
drugbydrop = drug * dropout;
center male severe0 (grandmean);
ANALYSIS:
estimator = ml;
link = probit;
integration = montecarlo;
MODEL:
! sequential specification for predictors;
dropout on male;
[dropout$1] (dthresh);
drug on dropout male;
severe0 on drug male dropout;
! label coefficients in focal model;
[severe6] (b0obs);
severe6 on dropout (b0diff)
	drug (b1obs)
	drugbydrop (b1diff)
	severe0 (b2)
 	male (b3);
severe6 (resvar);
! factored (sequential) specification for auxiliary variables;
severe1 on severe6 drug severe0 male;
severe3 on severe1 severe6 drug severe0 male;
MODEL CONSTRAINT:
! define new parameters
new(cohensd pobs pmis b0mis b1mis b0 b1);
! set b0diff equal to +.20 residual std. dev. units
! set b1diff equal to -.20 residual std. dev. units
cohensd = .20;
b0diff = cohensd * sqrt(resvar);
b1diff = - cohensd * sqrt(resvar);
! missingness group proportions
pobs = 1 - phi(-dthresh);
pmis = phi(-dthresh);
! compute weighted average intercept and slope
b0mis = b0obs + b0diff;
b1mis = b1obs + b1diff;
b0 = (b0obs * pobs) + (b0mis * pmis);
b1 = (b1obs * pobs) + (b1mis * pmis);
OUTPUT:
stdyx residual;
